apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
	compileSdkVersion 29

	defaultConfig {
		applicationId "com.dontflymoney.view"

		// read the explanation
		// at the end of the file
		//noinspection HighAppVersionCode
		versionCode 2011000042
		versionName "4.1.4.6"

		minSdkVersion 21
		targetSdkVersion 29

		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		release {
			storeFile file("../../.circleci/android/ci.keystore")
			storePassword System.getenv("STORE_PASSWORD")
			keyAlias "DFM"
			keyPassword System.getenv("KEY_PASSWORD")
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
			signingConfig signingConfigs.release
			zipAlignEnabled true
			resValue 'string', 'app_short_name', 'DfM'
		}
		debug {
			applicationIdSuffix '.debug'
			resValue 'string', 'app_short_name', 'DfM Test'
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
	}

	testOptions {
		unitTests {
			includeAndroidResources = true
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
	}
}

dependencies {
	implementation "com.google.android.gms:play-services-iid:$play_version"
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
	implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
	implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

	testImplementation "junit:junit:$junit_version"
	testImplementation "org.mockito:mockito-core:$mockito_version"
	testImplementation "org.robolectric:robolectric:$robolectric_version"
	testImplementation "com.squareup.okhttp3:mockwebserver:$mockserver_version"
	androidTestImplementation "com.android.support.test:runner:$testrunner_version"
	androidTestImplementation "com.android.support.test.espresso:espresso-core:$expresso_version"
}

apply plugin: 'kotlin-android-extensions'

task wrapper(type: Wrapper) {
	gradleVersion = '6.2'
}

/*
I did not understood the meaning of this number until it was too late.
So, when I hit limit, I just make a really really really ugly
work-a-round, yet not knowing the real meaning of all of this:

> Cannot put
> 003 000 000 000
> replaced old pattern
> New Max Min Sys
> by
> 0020 ne ma mi sy

But I did regret. Version Code is a number to control the sequence, to
guarantee that the user is installing a newer version of the APK, and
should be just increased by one each time.

I cannot fix totally the mess I did. The last version code I put on
Play Console is 2004010202. The next one MUST be bigger than this.

The versions that had an android apk published until now were:
2.0.0.0  2.0.1.0  2.0.3.1  2.1.3.2  2.1.4.1  3.0.0.3  4.0.1.3  4.1.1.3
2.0.0.1  2.0.1.1  2.1.0.0  2.1.3.3  3.0.0.0  3.0.0.5  4.1.0.1  4.1.2.0
2.0.0.2  2.0.2.0  2.1.3.0  2.1.3.4  3.0.0.1  4.0.1.1  4.1.1.0  4.1.2.1
2.0.0.3  2.0.3.0  2.1.3.1  2.1.4.0  3.0.0.2  4.0.1.2  4.1.1.2  4.1.2.2

32 versions. I have to use a number greater than the last one.
The first DfM commit were at 2011. It sounds fair the number starts
with 2011.

I cannot put version code 33 anymore.
But it can be 2011000033.

I will increase the version name according to site version anytime
I publish anything, but the version code will only be increased when
a new version of the app is published.
*/
